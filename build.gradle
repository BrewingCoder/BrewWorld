buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath group: 'gradle.plugin.com.matthewprenger', name: 'CurseGradle', version: "1.+"
    }
}
plugins {
    id "com.wynprice.cursemaven" version "2.1.1"
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.matthewprenger.cursegradle'

version = mod_version
group = mod_group

archivesBaseName = mod_name
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'snapshot', version: '20200514-1.15.1'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                brewworld {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                brewworld {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'brewworld', '--all', '--output', file('src/generated/resources/')
            mods {
                brewworld {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven { url = "http://dvs1.progwml6.com/files/maven" } //JEI
    maven { url = "http://maven.tterrag.com" } //TOP
    maven { url = "https://maven.blamejared.com" } //Craft Tweaker
    maven { url = "https://maven.theillusivec4.top" } //Curios
    maven { url = "https://maven.gscottsingleton.com/repository/mcrfl/"}  //ours
    maven { url 'https://minecraft.curseforge.com/api/maven' }
    maven { url 'https://jitpack.io/' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.15.2-31.2.0'
    compileOnly fg.deobf("mezz.jei:jei-1.15.2:6.0.0.2:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.15.2:6.0.0.2")
    compile fg.deobf("com.brewingcoder:BrewTeamLib:${brewteamlib_version}")

    compileOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-1.15:${top_version}:api")
    runtimeOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-1.15:${top_version}")

    compile fg.deobf("com.blamejared.crafttweaker:CraftTweaker-1.15.1:${crafttweaker_version}")
    runtime fg.deobf(project.dependencies.create("curse.maven:mekanism:2938498") {
        transitive = false
    })
}

jar {
    manifest {
        attributes([
            "Specification-Title": mod_name,
            "Specification-Vendor": mod_group,
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": mod_name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :mod_group,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}


jar.finalizedBy('reobfJar')

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task deobfJar(type:Jar) {
    from sourceSets.main.output
    classifier 'deobf'
}

artifacts {
    archives jar
    archives sourcesJar
    archives deobfJar
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = mod_group
            artifactId = project.archivesBaseName
            version = mod_version
            artifact jar
            artifact sourcesJar
            artifact deobfJar
            artifact apiJar
        }
    }
    repositories {
        maven {
            credentials {
                username project.hasProperty('maven_username') ? project.property('maven_username') : ''
                password project.hasProperty('maven_password') ? project.property('maven_password') : ''
            }
            url "https://maven.gscottsingleton.com/repository/mcrfl/"
        }
    }
}